import java.util.*

pluginManagement {
    repositories {
        mavenLocal()
        maven("https://maven.aliyun.com/repository/gradle-plugin") // Maven Central blocks CNB/CN hosts (HTTP 403)
        maven("https://maven.aliyun.com/repository/public")
        // Must come before gradlePluginPortal(): the weaver plugin's transitive
        // paperweight-core dependency only exists here; the portal would proxy the
        // lookup to Maven Central, which 403s CNB/CN egress IPs and aborts resolution.
        maven {
            name = "canvasmc"
            url = uri("https://maven.canvasmc.io/public")
        }
        gradlePluginPortal()
    }
}

plugins {
    id("org.gradle.toolchains.foojay-resolver-convention") version "1.0.0"
}

if (!file(".git").exists()) {
    val errorText = """
        
        =====================[ ERROR ]=====================
         The Petiole project directory is not a properly cloned Git repository.
         
         In order to build Petiole from source you must clone
         the Petiole repository using Git, not download a code
         zip from GitHub.
         
         Built Petiole jars are available for download at
         https://github.com/wosnxn123/Petiole/releases
         
         See https://github.com/wosnxn123/Petiole/blob/HEAD/CONTRIBUTING.md
         for further information on building and modifying Canvas.
        ===================================================
    """.trimIndent()
    error(errorText)
}

enableFeaturePreview("TYPESAFE_PROJECT_ACCESSORS")

rootProject.name = "Petiole"
for (name in listOf("petiole-api", "petiole-server")) {
    val projName = name.lowercase(Locale.ENGLISH)
    include(projName)
    findProject(":$projName")!!.projectDir = file(name)
}

rootDir.listFiles()
    ?.filter { it.isDirectory && (it.name.endsWith("-debug", ignoreCase = true) || it.name.endsWith("-plugin", ignoreCase = true)) }
    ?.forEach { dir ->
        val projName = dir.name.lowercase(Locale.ENGLISH)
        include(projName)
        findProject(":$projName")!!.projectDir = dir
    }

gradle.lifecycle.beforeProject {
    val mcVersion = providers.gradleProperty("mcVersion").get().trim()
    val petioleChannel = providers.gradleProperty("channel").get().trim()
    val petioleBuildNumber = providers.environmentVariable("BUILD_NUMBER").orNull?.trim()?.toInt()
    val versionString = if (petioleBuildNumber == null) {
        "$mcVersion.local-SNAPSHOT"
    } else {
        "$mcVersion.build.$petioleBuildNumber-${petioleChannel.lowercase()}"
    }
    version = versionString
}
